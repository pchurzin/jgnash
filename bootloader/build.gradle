plugins {
    id "edu.sc.seis.launch4j"
    id "org.openjfx.javafxplugin"
}

apply plugin: "distribution"
apply plugin: "application" // creates a task to run the full application

description = "jGnashFx BootLoader"
mainClassName = "jGnash"

javafx {
    version = javaFXVersion
    modules = [ "javafx.controls"]
}

dependencies {
    compile project(":jgnash-fx")
}

launch4j {
    copyright = "Copyright (C) 2001-2019 Craig Cavanaugh"
    mainClassName = "jGnash"
    outfile = "jGnash.exe"
    dontWrapJar = true
    icon = "../../../jgnash-fx/gnome-money.ico"
    downloadUrl = "https://www.azul.com/downloads/zulu/"
    bundledJrePath = "%JAVA_HOME%"  // work around for not being able to find installed jre, https://sourceforge.net/p/launch4j/bugs/197/
    jreMinVersion = "11"
}

task createStartScripts(type: CreateStartScripts) {
    outputDir = file("../../")
}

// call launch4j prior to creating the zip file
distZip.dependsOn(createExe)

/*distZip {
    doLast {
        file("$destinationDir/$archiveName").renameTo("$destinationDir/"+"jGnash-"+version+".zip")
    }
}*/

distributions {
    main {
        baseName = "jGnash"
        contents {
            from { "../jgnash-manual/src/Manual.pdf" }
            from { "../changelog.adoc" }
            from { "build/launch4j/jGnash.exe" }
            from { "../README.html" }
            from { "../README.adoc" }
            from { "../jGnash" }
            exclude("**/*-linux*")  // excludes linux specific JavaFx modules from cross platform zip
            exclude("**/*-win*")    // excludes windows specific JavaFx modules from cross platform zip
            exclude("**/*-mac*")    // excludes mac specific JavaFx modules from cross platform zip
        }
    }
}

jar {
    // Keep jar clean:
    exclude "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA", "META-INF/*.MF"

    manifest {
        attributes  "Main-Class": "jGnash",
                "Class-Path": configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(" ")
    }
}