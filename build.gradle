plugins {
    id "com.github.ben-manes.versions"
}

allprojects {
    apply plugin: "java"

    sourceCompatibility = 11
    targetCompatibility = 11

    group = "jgnash"
    version = "3.3.0"

    project.ext {
        junitVersion = "5.5.2"              // https://junit.org/junit5/
        junitextensionsVersion = "2.3.0"    // https://github.com/glytching/junit-extensions
        awaitilityVersion = "4.0.1"         // https://github.com/awaitility/awaitility

        picocliVersion = "4.0.4"            // https://github.com/remkop/picocli

        apachePoiVersion = "4.1.0"          // https://poi.apache.org/
        commonsCsvVersion = "1.7"           // https://commons.apache.org/proper/commons-csv/
        commonsCollectionsVersion = "4.4"   // https://commons.apache.org/proper/commons-collections/
        commonsTextVersion = "1.8"          // https://commons.apache.org/proper/commons-text/
        commonsLangVersion = "3.9"          // https://commons.apache.org/proper/commons-lang/

        h2Version = "1.4.199"               // http://www.h2database.com/html/main.html
        hsqldbVersion = "2.4.1"             // http://hsqldb.org/

        xstreamVersion = "1.4.11.1"         // https://github.com/x-stream/xstream

        hibernateVersion = "5.4.5.Final"    // http://hibernate.org/orm/
        hikariVersion = "3.4.1"             // https://github.com/brettwooldridge/HikariCP

        pdfBoxVersion = "2.0.17"            // https://pdfbox.apache.org/

        nettyVersion = "4.1.41.Final"       // https://netty.io/index.html

        slf4jVersion = "1.8.0-beta4"        // https://www.slf4j.org/news.html

        javaFXVersion = "13"
    }
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }

    dependencies {
        testCompile group: "org.junit.jupiter", name: "junit-jupiter-api", version: junitVersion
        testCompile group: "org.junit.jupiter", name: "junit-jupiter-params", version: junitVersion
        testRuntime group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion

        testCompile group: "io.github.glytching", name: "junit-extensions", version: junitextensionsVersion
        testCompile group: "org.awaitility", name: "awaitility", version: awaitilityVersion
        testCompile group: "org.apache.commons", name: "commons-text", version: commonsTextVersion
    }

    test {
        useJUnitPlatform()

        //we want display the following test events
        testLogging {
            events "PASSED", "STARTED", "FAILED", "SKIPPED"
            showStandardStreams = true
        }
    }
}

wrapper {
    gradleVersion = "5.6.2"
}

task distZip {
    doLast {
        ant.zip(destfile: "jgnash-" + version + "-bin.zip", duplicate: "preserve", update: "false") {
            zipgroupfileset(dir: ".", includes: "jgnash-fx/build/distributions/jGnashFx-" + version + ".zip")
            zipgroupfileset(dir: ".", includes: "bootloader/build/distributions/jGnash-" + version + ".zip")
        }
    }
}

distZip.dependsOn(":jgnash-fx:distZip")
distZip.dependsOn(":bootloader:distZip")
