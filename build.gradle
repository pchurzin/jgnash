plugins {
    id "com.github.ben-manes.versions" version "0.20.0"
}

allprojects {

    // jgnash-help is not a compiled sub-project
    if (!it.name.startsWith("jgnash-help")) {
        apply plugin: "java"
    }
    
    sourceCompatibility = 11
    targetCompatibility = 11

    group = "jgnash"
    version = "3.0.2"

    project.ext {
        junitVersion = "5.4.1"
        junitextensionsVersion = "2.3.0"    // https://github.com/glytching/junit-extensions
        awaitilityVersion = "3.1.6"         // https://github.com/awaitility/awaitility

        picocliVersion = "3.9.5"

        apachePoiVersion = "4.0.1"
        commonsCsvVersion = "1.6"
        commonsTextVersion = "1.6"
        commonsLangVersion = "3.8.1"

        h2Version = "1.4.199"
        hsqldbVersion = "2.4.1"

        xstreamVersion = "1.4.11.1"

        hibernateVersion = "5.4.2.Final"
        hikariVersion = "3.3.1"             // https://github.com/brettwooldridge/HikariCP
        
        pdfBoxVersion = "2.0.14"

        nettyVersion = "4.1.34.Final"

        slf4jVersion = "1.8.0-beta4"
    }
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
        maven { url "http://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/" }
    }

    // jgnash-help is not a compiled sub-project
    if (!it.name.startsWith("jgnash-help")) {
        dependencies {

            testCompile group: "org.junit.jupiter", name: "junit-jupiter-api", version: junitVersion
            testCompile group: "org.junit.jupiter", name: "junit-jupiter-params", version: junitVersion
            testRuntime group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion

            testCompile group: "io.github.glytching", name: "junit-extensions", version: junitextensionsVersion
            testCompile group: "org.awaitility", name: "awaitility", version: awaitilityVersion
            testCompile group: "org.apache.commons", name: "commons-text", version: commonsTextVersion
        }
    }

    test {
        useJUnitPlatform()

        //we want display the following test events
        testLogging {
            events "PASSED", "STARTED", "FAILED", "SKIPPED"
            showStandardStreams = true
        }
    }
}

wrapper {
    gradleVersion = "5.3.1"
}

task distZip {
    doLast {
        ant.zip(destfile: "jgnash-" + version + "-bin.zip", duplicate: "preserve", update: "false") {
            zipgroupfileset(dir: ".", includes: "jgnash-fx/build/distributions/jGnashFx-" + version + ".zip")
            zipgroupfileset(dir: ".", includes: "bootloader/build/distributions/jGnash-" + version + ".zip")
        }
    }
}

distZip.dependsOn(":jgnash-fx:distZip")
distZip.dependsOn(":bootloader:distZip")
