plugins {
    id "org.openjfx.javafxplugin" version "0.0.7"
}

apply plugin: "distribution"
apply plugin: "application" // creates a task to run the full application

description = "jGnash JavaFx"
mainClassName = "jGnashFx"

dependencies {
    compile project(":jgnash-help")
    compile project(":jgnash-core")
    compile project(":jgnash-convert")
    compile project(":jgnash-report-core")
    compile project(":jgnash-plugin")

    compile group: "info.picocli", name: "picocli", version: picocliVersion

    // Hack to include all javafx platforms in the classpath
    // The platform specific libraries are excluded when distributions is assembled
    compile 'org.openjfx:javafx-base:12.0.1'
    compile 'org.openjfx:javafx-controls:12.0.1'
    compile 'org.openjfx:javafx-fxml:12.0.1'
    compile 'org.openjfx:javafx-graphics:12.0.1'
    compile 'org.openjfx:javafx-media:12.0.1'
    compile 'org.openjfx:javafx-swing:12.0.1'
    compile 'org.openjfx:javafx-web:12.0.1'

    compile 'org.openjfx:javafx-base:12.0.1:linux'
    compile 'org.openjfx:javafx-controls:12.0.1:linux'
    compile 'org.openjfx:javafx-fxml:12.0.1:linux'
    compile 'org.openjfx:javafx-graphics:12.0.1:linux'
    compile 'org.openjfx:javafx-media:12.0.1:linux'
    compile 'org.openjfx:javafx-swing:12.0.1:linux'
    compile 'org.openjfx:javafx-web:12.0.1:linux'

    compile 'org.openjfx:javafx-base:12.0.1:win'
    compile 'org.openjfx:javafx-controls:12.0.1:win'
    compile 'org.openjfx:javafx-fxml:12.0.1:win'
    compile 'org.openjfx:javafx-graphics:12.0.1:win'
    compile 'org.openjfx:javafx-media:12.0.1:win'
    compile 'org.openjfx:javafx-swing:12.0.1:win'
    compile 'org.openjfx:javafx-web:12.0.1:win'

    compile 'org.openjfx:javafx-base:12.0.1:mac'
    compile 'org.openjfx:javafx-controls:12.0.1:mac'
    compile 'org.openjfx:javafx-fxml:12.0.1:mac'
    compile 'org.openjfx:javafx-graphics:12.0.1:mac'
    compile 'org.openjfx:javafx-media:12.0.1:mac'
    compile 'org.openjfx:javafx-swing:12.0.1:mac'
    compile 'org.openjfx:javafx-web:12.0.1:mac'
    // end hack
}

javafx {
    modules = [ "javafx.base", "javafx.controls", "javafx.fxml", "javafx.web", "javafx.swing", "javafx.graphics", "javafx.media"]
    version = "12.0.1"
}

startScripts.enabled = false

// build the mt940 plugin prior to creating the zip file without creating a circular loop
distZip.dependsOn(":mt940:build")

// ensure the manual is built and included
distZip.dependsOn(":jgnash-help:asciidoctor")

distZip {
    into("jGnash-$version") {
        from "."
        include "scripts/*"
    }

    into("jGnash-$version") {
        from "../mt940/build/libs"
        include "*"
        into "jGnash-$version/plugins"
    }

    doLast {
        file("$destinationDir/$archiveName").renameTo("$destinationDir/"+"jGnashFx-"+version+".zip")
    }
}

distributions {
    main {
        baseName = "jGnash"
        contents {
            exclude("**/*-linux*")  // excludes linux specific JavaFx modules from cross platform zip
            exclude("**/*-win*")    // excludes windows specific JavaFx modules from cross platform zip
            exclude("**/*-mac*")    // excludes mac specific JavaFx modules from cross platform zip
        }
    }
}

jar {
    // Keep jar clean:
    exclude "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA", "META-INF/*.MF"

    manifest {
        attributes  "Main-Class": "jGnashFx",
                    "Class-Path": configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(" ")
    }
}
